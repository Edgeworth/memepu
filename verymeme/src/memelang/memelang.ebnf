grammar memelang;
file : (intf_defn | struct_defn | fn_defn | enum_defn | impl_defn)* EOF;

// Top level constructs:
intf_defn : 'intf' typename intf_blk ;
struct_defn : 'struct' typename struct_blk ;
fn_defn : fn_signature stmt_blk ;
enum_defn : 'enum' typename enum_blk ;
impl_defn : 'impl' typelist? typename 'for' typename impl_blk ;

// Block related:
stmt_blk : LBRACE stmt* RBRACE ;
struct_blk : LBRACE ('static'? fn_defn | struct_var_decl ';')* RBRACE ;
impl_blk : LBRACE fn_defn* RBRACE ;
intf_blk : LBRACE fn_decl* RBRACE ;
// TODO: Support structure like objects?
enum_blk : LBRACE (IDENT ('(' type ')')? ',')* RBRACE ;
// TODO: Ranges / OR / values / tuples / destructuring?
match_blk : LBRACE ((IDENT ('(' IDENT ')')? | literal) stmt_blk)* RBRACE ;

// Statement related:
stmt : expr ';' | stmt_var_defn | stmt_var_decl ';' | stmt_ret | stmt_for | stmt_if | stmt_match | ASM ;
stmt_ret : 'return' expr ';' ;
stmt_for : 'for' stmt_var_defn expr ';' expr stmt_blk ;
stmt_if : 'if' expr stmt_blk ('else' stmt_if)* ('else' stmt_blk)? ;
stmt_match : 'match' expr match_blk ;

// Variable related:
struct_var_decl : IDENT ':' type (':' int_lit)? ;  // Optional bitfield.
stmt_var_decl : 'var' IDENT ':' ('const'? 'auto' | type);
fn_var_decl : IDENT ':' type;
stmt_var_defn : stmt_var_decl '=' expr ';' ;

// Expression related:
literal : STR_LIT | CHAR_LIT | int_lit | float_lit | bool_lit | struct_lit | array_lit;
struct_lit : type LBRACE (IDENT ':' expr ',')* RBRACE ;
array_lit : '[]' | '[' expr (',' expr)* ']';
expr : expr '[' expr ']'  // Array subscript
    | fn_call  // Function call
    | expr '.' (fn_call | IDENT)  // Member access
    | expr '?'  // Result syntactic sugar
    | expr postfix_op  // Postfix operations
    | <assoc=right> prefix_op expr  // Prefix operations
    | expr binary_op expr  // Binary operations
    | <assoc=right> expr '?' expr ':' expr  // Ternary expression
    | <assoc=right> expr '=' expr  // Assignment
    | '(' expr ')' // Grouped expression
    | IDENT | type | literal ;

// Type related:
typebound : IDENT ; // TODO: Support complex type bounds?
typelist : '<' typebound (',' typebound)* '>' ;
typename : IDENT typelist? ;
// Identifiers either a concrete type or a set of types that should be deduced to a single type.
qualifier : 'const'? '*' | '[' int_lit ']';
type : qualifier* 'const'? (IDENT ('<' type (',' type)* '>')? | builtin_type);

// Function related:
fn_decl : fn_signature ';' ; // Only allowed in interfaces.
fn_signature : 'fn' typename
    ('()' | '(' fn_var_decl (',' fn_var_decl)* ')') type ;
fn_call : type ('()' | '(' expr (',' expr)* ')') ;

// Ideas:
// Switch case (matching?)

// Terminals:
builtin_type : 'i8' | 'i16' | 'i32' | 'u8' | 'u16' | 'u32' | 'bool' | 'bit' ;
// Parse << and >> as two separate tokens to allow stuff like vector<vector<T>>.
binary_op : '*' | '/' | '%' | '+' | '-' | '>' '>' | '<' '<' | '<=' | '>=' | '<' | '>'
    | '==' | '!=' |  '&' | '^' | '|' | '&&' | '||';
prefix_op : '~' | '-' | '!' | '&' | '*' ;
postfix_op : '+' '+' | '-' '-' ;
NUMBERS : [0-9]+ ;
int_lit : '0x'? NUMBERS;
float_lit : NUMBERS '.' NUMBERS ;
CHAR_LIT  : '\'' (~[\t\r\n] | ESCAPE) '\'' ;
STR_LIT : ('"' ~[\t\r\n]* '"')+ ;  // Allow literal concat.
bool_lit : 'false' | 'true' ;
WHITESPACE : [ \t\r\n]+ -> skip ;
COMMENT : '//' ~[\n]* -> skip;
IDENT : [a-zA-Z_]+[a-zA-Z0-9_]* ;
LBRACE : '{' ;
RBRACE : '}' ;
ASM : 'asm' [ \t\r\n]* '{' ~[}]* '}' ;
ESCAPE : '\\' [\\'"nrt] ;
